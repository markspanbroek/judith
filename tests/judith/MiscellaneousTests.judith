# Test parent

object foo := Object |[

  method test1 [
    # weirdness number 1: you would expect: Assert.that(parent = Object).isTrue
    # might be avoided by using 'super' instead of 'parent'
    Assert.that(parent = self).isTrue
  ]

  method test2 [
    self.test2a
  ]

  method test3a [
    self.test3b
  ]

]|
foo.test1

object bar := foo |[

  method test2a [
    # skip
  ]

  method test3 [
    parent.test3a
  ]

  method test3a [
    Exception.throw
  ]

  method test3b [
    #skip
  ]

  method parent [
    result := parent
  ]

]|
bar.test2
bar.test3

#object baz := bar |[
#
#  method test4 [
#    #self.parent.test3a
#    # Weirdness number 2: the following produces an error, while the statement
#    # above doesn't:
#     self.parent|[object foo := foo]|.test3a
#  ]
#
#]|
#baz.test4


##
# Replacement tests
#
object replacementTest1 := Object |[

  method identify [
    result := 1
  ]

]|

object replacementTest2 := replacementTest1 |[

  method identify [
    result := parent.identify + 1
  ]

]|

object replacementTest3 := replacementTest1 |[

  method identify [
    result := 3
  ]

]|

Assert.that(replacementTest1.identify = 1).isTrue
Assert.that(replacementTest2.identify = 2).isTrue
replacementTest1.replace(replacementTest3)
Assert.that(replacementTest1.identify = 3).isTrue
Assert.that(replacementTest2.identify = 4).isTrue


object Point := Object
|[
  object x := 0
  object y := 0
  
  method create(x',y') 
  [
    result := self.copy.initialize(x',y')
  ]
  
  method initialize(x',y') 
  [
    Assert.that(x' : Objects.Number).isTrue
    Assert.that(y' : Objects.Number).isTrue

    x := x'
    y := y'
  ]
  
  method x 
  [
    result := x
  ]
  
  method y 
  [
    result := y
  ]  
]|

Point := Point
|[
  method translate(dx,dy)
  [
    Assert.that(dx : Objects.Number).isTrue
    Assert.that(dy : Objects.Number).isTrue
    
    result := self.create(self.x + dx, self.y + dy)
  ]
]|

Point.create(1,2).translate(-1,-2).x

ExceptionHandler |[

  method try [
    Point.initialize(1,2)
    Assert.that(false).isTrue
  ]
  
  method catch(exception) [
    # skip
  ]
  
]|.run


