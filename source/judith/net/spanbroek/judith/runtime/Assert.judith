[
    object AssertionFailed := Exception
    |[
        method asText
        [
            if self.message = ""
                result := "Assertion failed."
            || true
                result := "Assertion failed: " + self.message
            fi
        ]
    ]|

    object Assertion := Object
    |[
        object subject := null
        
        method setSubject(subject')
        [
            subject := subject'
        ]
        
        method create(subject)
        [
            result := self.copy.setSubject(subject)
        ]

        method isTrue
        [
            if not subject
                AssertionFailed.throw
            fi
        ]        
        
        method equals(expected)
        [
            if not (expected = subject)
                object message := "Expected '" + expected.asText + "'"
                message := message + " but got '" + subject.asText + "'"
                AssertionFailed.copy.setMessage(message).throw
            fi
        ]

        method isNotEqualTo(unexpected)
        [
            if unexpected = subject
                object message := "Unexpected '" + subject.asText + "'"
                AssertionFailed.copy.setMessage(message).throw
            fi
        ]

        method throws(expectedException)
        [
            object exception := null
            ExceptionHandler
            |[
                method try
                [
                    subject.execute
                ]
                
                method catch(exception')
                [
                    exception := exception'
                ]
            ]|.run
            
            if not (exception : expectedException)
                object message := "Expected exception to occur"
                AssertionFailed.copy.setMessage(message).throw
            fi
        ]
        
        method isNotNull
        [
            if subject = null
                object message := "Expected non-null object"
                AssertionFailed.copy.setMessage(message).throw
            fi
        ]
        
        method isNull
        [
            if not (subject = null)
                object message := "Expected null"
                AssertionFailed.copy.setMessage(message).throw
            fi
        ]
    ]|

    object Assert := Object
    |[
        method that(assertion) 
        [
            result := Assertion.create(assertion)
        ]
    ]|

    Objects := Objects
    |[
        method Assert
        [
            result := Assert
        ]

        method AssertionFailed
        [
            result := AssertionFailed
        ]
    ]|
]

