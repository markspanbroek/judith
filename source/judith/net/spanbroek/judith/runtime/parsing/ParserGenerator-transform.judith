[
    object ParserGenerator := Objects.Parsing.ParserGenerator
    object Parser := Objects.Parsing.Parser

    object TransformingParser := Parser
    |[
        object operand := null
        object transform := null

        method setOperand(operand')
        [
            operand := operand'
        ]

        method setTransform(transform')
        [
            transform := transform'
        ]

        method run(context, index, resultBeforeParsing, continuation)
        [
            object afterParsingDo := Function
            |[
                method run(indexAfterParsing, resultAfterParsing)
                [
                    object section := resultAfterParsing.section(resultBeforeParsing.length, resultAfterParsing.length)
                    context.doLater([continuation.run(indexAfterParsing, resultBeforeParsing.copy.push(transform.evaluate(section)))])
                ]
            ]|
            context.doLater([operand.run(context, index, resultBeforeParsing, afterParsingDo)])
        ]
    ]|

    ParserGenerator.replace(ParserGenerator
    |[
        method transform(operand, transform)
        [
            result :=
                TransformingParser
                    .copy
                    .setOperand(operand)
                    .setTransform(transform)
        ]
    ]|)
]
