[
    object ParserGenerator := Objects.Parsing.ParserGenerator
    object Parser := Objects.Parsing.Parser
    object List := Objects.List

    object TransformingParser := Parser
    |[
        object operand := null
        object transform := null

        method setOperand(operand')
        [
            operand := operand'
        ]

        method setTransform(transform')
        [
            transform := transform'
        ]

        method run(context, index, resultBeforeParsing, continuation)
        [
            object afterParsingDo := Function
            |[
                method run(indexAfterParsing, resultAfterParsing)
                [
                    object transformedResult := transform.evaluate(resultAfterParsing)
                    context.doLater([continuation.run(indexAfterParsing, resultBeforeParsing.copy.push(transformedResult))])
                ]
            ]|
            context.doLater([operand.run(context, index, List.copy, afterParsingDo)])
        ]
    ]|

    ParserGenerator.replace(ParserGenerator
    |[
        method transform(operand, transform)
        [
            result :=
                TransformingParser
                    .copy
                    .setOperand(operand)
                    .setTransform(transform)
        ]
    ]|)
]
